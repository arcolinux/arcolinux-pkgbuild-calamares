#https://github.com/calamares/calamares/releases
#change prepare number too

pkgname=arco-calamares-wayland
_pkgname=calamares
pkgver=3.3.6
pkgrel=02
pkgdesc='Distribution-independent installer framework'
arch=('i686' 'x86_64')
license=(GPL)
url="https://github.com/calamares/calamares/releases"
license=('LGPL')
#'appstream-qt5' out as dependency and on iso
depends=(
	'boost-libs'
	'ckbcomp'
	'efibootmgr'
	'gtk-update-icon-cache'
	'hwinfo'
	'icu'
	'kconfig5'
	'kcoreaddons5'
	'kdbusaddons5'
	'ki18n5'
	'kiconthemes5'
	'kio5'
	'kpmcore'
	'libpwquality'
	'mkinitcpio-openswap'
	'plasma-framework5'
	'polkit-qt5'
	'qt5-svg'
	'qt5-xmlpatterns'
	'solid5'
	'squashfs-tools'
	'yaml-cpp'
)
conflicts=(
	'arco-calamares'
	'arco-calamares-git'
	'arco-calamares-plasma'
)
makedepends=(
	'boost'
	'extra-cmake-modules'
	'git'
	'qt5-tools'
	'qt5-translations'
)

backup=('usr/share/calamares/modules/bootloader.conf'
        'usr/share/calamares/modules/displaymanager.conf'
        'usr/share/calamares/modules/initcpio.conf'
        'usr/share/calamares/modules/unpackfs.conf')

source=("calamares::https://github.com/erikdubois/calamares/archive/refs/tags/$pkgver-$pkgrel.tar.gz"
	"cal-arcolinux.desktop"
	"cal-arcolinux-debugging.desktop"
	"calamares_polkit")

sha256sums=('dd27344d3bd6e5a1e514b8cb297bf14d832dba45ae9f908aa622ad243ca57081'
            'ad204bdf8fd967bc0397d22b6ee9e6249c76624fba1469d1ccffbb81ed763c79'
            '54fc7e06fb3b143d29880011a604dc1b2b8d9ecedfcc4d25c045f3e46a3cce09'
            'f782a476986095a0fdebee2e6ef695f56776d6d3386228835ce0511ee71b4e0c')

prepare() {

	cp -rv ../modules/* ${srcdir}/$_pkgname-${pkgver}-${pkgrel}/src/modules/

	sed -i -e 's/"Install configuration files" OFF/"Install configuration files" ON/' "$srcdir/${_pkgname}-${pkgver}-${pkgrel}/CMakeLists.txt"
	sed -i -e "s/desired_size = 512 \* 1024 \* 1024  \# 512MiB/desired_size = 512 \* 1024 \* 1024 \* 4  \# 2048MiB/" "$srcdir/${_pkgname}-${pkgver}-${pkgrel}/src/modules/fstab/main.py"

	cd ${_pkgname}-${pkgver}-${pkgrel}
	sed -i -e "s|CALAMARES_VERSION 3.3.5|CALAMARES_VERSION $pkgver-$pkgrel|g" CMakeLists.txt
}

build() {
	cd $_pkgname-$pkgver-${pkgrel}

	mkdir -p build
	cd build
	cmake .. \
	-DCMAKE_BUILD_TYPE=Release \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DCMAKE_INSTALL_LIBDIR=lib \
	-DWITH_PYTHONQT=OFF \
	-DWITH_KF5DBus=OFF \
	-DBoost_NO_BOOST_CMAKE=ON \
	-DWEBVIEW_FORCE_WEBKIT=OFF \
	-DSKIP_MODULES="dracut dracutlukscfg dummycpp dummyprocess dummypython \
	dummypythonqt initramfs initramfscfg interactiveterminal \
	keyboardq license localeq oemid packagechooserq partitionq \
	services-openrc tracking webview welcomeq"
	make
}

package() {
	cd ${_pkgname}-${pkgver}-${pkgrel}/build
	make DESTDIR="$pkgdir" install
	install -Dm644 "$srcdir/cal-arcolinux.desktop" "$pkgdir/usr/share/applications/cal-arcolinux.desktop"
	install -Dm644 "$srcdir/cal-arcolinux-debugging.desktop" "$pkgdir/usr/share/applications/cal-arcolinux-debugging.desktop"
	install -Dm755 "$srcdir/calamares_polkit" "$pkgdir/usr/bin/calamares_polkit"
	rm "$pkgdir/usr/share/applications/calamares.desktop"
}
