# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Gerardo Exequiel Pozzi <djgera@archlinux.org>

pkgbase=archiso
pkgname=('archiso-git' 'mkinitcpio-archiso-git')
pkgver=v50.1.r0.g8ba2870
pkgrel=1
pkgdesc='Tools for creating Arch Linux live and install iso images'
arch=('any')
conflicts=('archiso' 'mkinitcpio-archiso-git')
license=('GPL3')
url="https://gitlab.archlinux.org/archlinux/archiso"
makedepends=('git')
source=("git+https://gitlab.archlinux.org/archlinux/archiso.git")
sha256sums=('SKIP')

pkgver() {
  cd ${srcdir}/$pkgbase
  # cutting off 'foo-' prefix that presents in the git tag
  git describe --long | sed 's/^foo-//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

package_archiso-git() {
  depends=('arch-install-scripts' 'bash' 'dosfstools' 'e2fsprogs' 'libisoburn'
  'mtools' 'squashfs-tools')
  optdepends=('edk2-ovmf: for emulating UEFI with run_archiso'
              'qemu: for run_archiso')
  cd "${pkgbase}"
  make DESTDIR="${pkgdir}" install
}

package_mkinitcpio-archiso-git() {
  pkgdesc='Mkinitcpio hooks and scripts for archiso'
  depends=('mkinitcpio')
  cd "${pkgbase}"
  make DESTDIR="${pkgdir}" install-initcpio
}
